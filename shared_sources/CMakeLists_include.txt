
set(VERSION "1.3.3")

set(SUPPORT_SOCKETS OFF CACHE BOOL "add a socket protocol so samples can be controled remotely")

set(SUPPORT_NVTOOLSEXT OFF CACHE BOOL "Use NSight for custom markers")
if(WIN32)
  SET( MEMORY_LEAKS_CHECK OFF CACHE BOOL "Check for Memory leaks" )
  SET(USE_GLFW OFF CACHE BOOL "Use GLFW instead of our own simple Window management")
else(WIN32)
  SET(USE_GLFW ON CACHE BOOL "Use GLFW instead of our own simple Window management")
endif(WIN32)

SET(RESOURCE_DIRECTORY "${BASE_DIRECTORY}/shared_sources/resources")
add_definitions(-DRESOURCE_DIRECTORY="${RESOURCE_DIRECTORY}/")

Message(STATUS "BASE_DIRECTORY = ${BASE_DIRECTORY}")
Message(STATUS "CMAKE_CURRENT_SOURCE_DIR = ${CMAKE_CURRENT_SOURCE_DIR}")

# Specify the list of directories to search for cmake modules.
set(CMAKE_MODULE_PATH
    ${BASE_DIRECTORY}/shared_sources/cmake
)

set( CMAKE_FIND_ROOT_PATH "")

if( CMAKE_SIZEOF_VOID_P EQUAL 8 )
  set (ARCH "x64" CACHE STRING "CPU Architecture")
else ()
  set (ARCH "x86" CACHE STRING "CPU Architecture")
endif()

set(EXECUTABLE_OUTPUT_PATH
    ${BASE_DIRECTORY}/bin_${ARCH}
    CACHE PATH
    "Directory where executables will be stored"
)

# Set the default build to Release.  Note this doesn't do anything for the VS
# default build target.
if(NOT CMAKE_BUILD_TYPE)
  set(CMAKE_BUILD_TYPE "Release" CACHE STRING
      "Choose the type of build, options are: Debug Release RelWithDebInfo MinSizeRel."
      FORCE)
endif(NOT CMAKE_BUILD_TYPE)

#####################################################################################
function(_make_relative FROM TO OUT)
  #message(STATUS "FROM = ${FROM}")
  #message(STATUS "TO = ${TO}")
  
  get_filename_component(FROM ${FROM} ABSOLUTE)
  get_filename_component(TO ${TO} ABSOLUTE)
  
  string(REPLACE "/" ";" FROM_LIST ${FROM})
  string(REPLACE "/" ";" TO_LIST ${TO})
  
  #message(STATUS "FROM = ${FROM_LIST}")
  #message(STATUS "TO = ${TO_LIST}")
  
  list(LENGTH FROM_LIST flen)
  math(EXPR flen "${flen} - 1" )
  #message(STATUS "flen = ${flen}")
  list(LENGTH TO_LIST tlen)
  math(EXPR tlen "${tlen} - 1" )
  #message(STATUS "tlen = ${tlen}")
  
  set(REL_LIST)
  foreach(loop_var RANGE ${flen})
    #message(STATUS "i = ${loop_var}")
    if ((loop_var GREATER tlen) OR (loop_var EQUAL tlen))
      list(APPEND REL_LIST "..")
      #message(STATUS "descend")
    else()
      list(GET FROM_LIST ${loop_var} f)
      list(GET TO_LIST ${loop_var} t)
      #message(STATUS "f = ${f}")
      #message(STATUS "t = ${t}")
      if (${f} STREQUAL ${t})
        set(begin ${loop_var})
        #message(STATUS "equal")
      else()
        list(APPEND REL_LIST "..")
        #message(STATUS "descend")
      endif()
    endif()
  endforeach(loop_var)
  math(EXPR begin "${begin} + 1" )
  
  #message(STATUS "---")
  
  foreach(loop_var RANGE ${begin} ${tlen})
    #message(STATUS "i = ${loop_var}")
    #message(STATUS "t = ${t}")
    #message(STATUS "ascend")
    list(GET TO_LIST ${loop_var} t)
    list(APPEND REL_LIST ${t})
  endforeach(loop_var)
  
  #message(STATUS "relative = ${REL_LIST}")

  string (REPLACE ";" "/" _TMP_STR "${REL_LIST}")
  set (${OUT} "${_TMP_STR}" PARENT_SCOPE)
endfunction()

macro(_add_project_definitions name)
  if(MSVC)
    _make_relative("${EXECUTABLE_OUTPUT_PATH}/config" "${CMAKE_CURRENT_SOURCE_DIR}" TOPROJECT)
  else()
    _make_relative("${EXECUTABLE_OUTPUT_PATH}" "${CMAKE_CURRENT_SOURCE_DIR}" TOPROJECT)
  endif()
  
  #message(STATUS "${TOPROJECT}")
  
  add_definitions(-DPROJECT_RELDIRECTORY="${TOPROJECT}/")
  add_definitions(-DPROJECT_ABSDIRECTORY="${CMAKE_CURRENT_SOURCE_DIR}/")
  add_definitions(-DPROJECT_NAME="${name}")  
  
  if (SUPPORT_NVTOOLSEXT)
    add_definitions(-DSUPPORT_NVTOOLSEXT)
  endif(SUPPORT_NVTOOLSEXT)
endmacro(_add_project_definitions)

#####################################################################################
if(UNIX) 
  set(OS "linux")
  add_definitions(-DLINUX)
else(UNIX)
  if(APPLE)
  else(APPLE)
    if(WIN32)
      set(OS "win")
      add_definitions(-DNOMINMAX)
      if(MEMORY_LEAKS_CHECK)
        add_definitions(-DMEMORY_LEAKS_CHECK)
      endif()
    endif(WIN32)
  endif(APPLE)
endif(UNIX)


if (MSVC90)
  include_directories(${BASE_DIRECTORY}/shared_external/stdint_old_msvc)
endif(MSVC90)


# Macro for adding files close to the executable
macro(_copy_files_to_target target thefiles)
    if(WIN32)
        foreach (FFF ${thefiles} )
          if(EXISTS "${FFF}")
            add_custom_command(
              TARGET ${target} POST_BUILD
              COMMAND ${CMAKE_COMMAND} -E copy_if_different
                ${FFF}
                $<TARGET_FILE_DIR:${target}>
                VERBATIM
            )
          endif()
        endforeach()
    endif()
endmacro()

#####################################################################################
# Optional OpenGL package
#
macro(_add_package_OpenGL)
  find_package(OpenGL)  
  if(OPENGL_FOUND)
      Message(STATUS "--> using package OpenGL")
      get_directory_property(hasParent PARENT_DIRECTORY)
      if(hasParent)
        set( USING_OPENGL "YES" PARENT_SCOPE) # PARENT_SCOPE important to have this variable passed to parent. Here we want to notify that something used the Vulkan package
      else()
        set( USING_OPENGL "YES")
      endif()
      add_definitions(-DUSEOPENGL)
    # ===============> GLEW
    if (NOT APPLE)
      find_package(GLEW REQUIRED)
      if(NOT GLEW_FOUND)
        message(WARNING "Try to set GLEW_LOCATION")
      else()
        include_directories(${GLEW_INCLUDE_DIR} )
      endif()
    endif()
    # ===============> GLFW
    if(USE_GLFW)
      find_package(GLFW)
      if(GLFW_FOUND AND USE_GLFW)
        include_directories(${GLFW_INCLUDE_DIR} )
      endif()
    endif()
 else(OPENGL_FOUND)
     Message(STATUS "--> NOT using package OpenGL")
 endif(OPENGL_FOUND)
endmacro(_add_package_OpenGL)
# this macro is needed for the samples to add this package, although not needed
# this happens when the shared_sources library was built with these stuff in it
# so many samples can share the same library for many purposes
macro(_optional_package_OpenGL)
  if(USING_OPENGL)
    _add_package_OpenGL()
  endif(USING_OPENGL)
endmacro(_optional_package_OpenGL)
#####################################################################################
# Optional UI using Windows MFC
#
macro(_add_package_ZLIB)
  if(EXISTS ${BASE_DIRECTORY}/shared_external/zlib)
    set(ZLIB_ROOT ${BASE_DIRECTORY}/shared_external/zlib)
  endif()
  Message(STATUS "--> using package ZLIB")
  find_package(ZLIB)
  if(ZLIB_FOUND)
      include_directories(${ZLIB_INCLUDE_DIR})
      LIST(APPEND PACKAGE_SOURCE_FILES
        ${ZLIB_HEADERS}
        )
      LIST(APPEND LIBRARIES_OPTIMIZED ${ZLIB_LIBRARY})
      LIST(APPEND LIBRARIES_DEBUG ${ZLIB_LIBRARY})
  else()
    Message(WARNING "ZLIB not available. setting NOGZLIB define")
    add_definitions(-DNOGZLIB)
  endif()
endmacro()
#####################################################################################
# Optional UI using Windows MFC
#
macro(_add_package_MFCUI)
  if(UNIX)
    Message(STATUS "--> package SvcMFCUI not available under Unix")
  else(UNIX)
    find_package(SvcMFCUI)
    Message(STATUS "--> using package SvcMFCUI")
    if(SVCMFCUI_FOUND)
        add_definitions(-DUSESVCUI)
        include_directories(${SVCMFCUI_INCLUDE_DIR})
        LIST(APPEND PACKAGE_SOURCE_FILES
          ${SVCMFCUI_HEADERS}
          #SvcMFCUI.h
          )
        source_group(SvcMFCUI FILES  
          ${SVCMFCUI_HEADERS}
          #SvcMFCUI.h
        )
   endif()
  endif(UNIX)
endmacro()
#####################################################################################
# AntTweakBar UI
#
macro(_add_package_AntTweakBar)
  Message(STATUS "--> using package AntTweakBar")
  find_package(AntTweakBar)
  if(ANTTWEAKBAR_FOUND)
    add_definitions(-DUSEANTTWEAKBAR)
    include_directories(${ANTTWEAKBAR_INCLUDE_DIR})
    LIST(APPEND PACKAGE_SOURCE_FILES 
      ${ANTTWEAKBAR_HEADERS}
    )
    LIST(APPEND LIBRARIES_OPTIMIZED ${ANTTWEAKBAR_LIB})
    LIST(APPEND LIBRARIES_DEBUG ${ANTTWEAKBAR_LIB})
    source_group(AntTweakBar FILES 
      ${ANTTWEAKBAR_HEADERS}
    )
  endif()
endmacro()
#####################################################################################
# OculusSDK package
#
macro(_add_package_OculusSDK)
  Message(STATUS "--> using package OculusSDK")
  find_package(OculusSDK)
  if(OCULUSSDK_FOUND)
    add_definitions(-DUSEOCULUSSDK)
    include_directories(${OCULUSSDK_INCLUDE_DIRS})
    LIST(APPEND LIBRARIES_OPTIMIZED ${OCULUSSDK_LIBS})
    LIST(APPEND LIBRARIES_DEBUG ${OCULUSSDK_LIBS_DEBUG})
  endif()
endmacro()
#####################################################################################
# OpenVRSDK package
#
macro(_add_package_OpenVRSDK)
  Message(STATUS "--> using package OpenVRSDK")
  find_package(OpenVRSDK)
  if(OPENVRSDK_FOUND)
    add_definitions(-DUSEOPENVRSDK)
    include_directories(${OPENVRSDK_INCLUDE_DIRS})
    LIST(APPEND LIBRARIES_OPTIMIZED ${OPENVRSDK_LIBS})
    LIST(APPEND LIBRARIES_DEBUG ${OPENVRSDK_LIBS})
  endif()
endmacro()
#####################################################################################
# nvFx package
#
macro(_add_package_nvFx)
  Message(STATUS "--> using package nvFX")
  find_package(nvFx)
  if(NVFX_FOUND)
    add_definitions(-DUSE_NVFX)
    LIST(APPEND LIBRARIES_OPTIMIZED ${NVFX_LIBRARIES} ${NVFX_LIBRARIES_GL} )
    LIST(APPEND LIBRARIES_DEBUG ${NVFX_LIBRARIES} ${NVFX_LIBRARIES_GL} )
    LIST(APPEND PACKAGE_SOURCE_FILES ${NVFX_HEADERS})
    source_group(nvFX FILES ${NVFX_HEADERS})
  endif()
endmacro()
#####################################################################################
#
macro(_add_package_OpenGLText)
  # commented: this is a small external tool that we can make local (in shared_sources), for now
  #if(NOT OPENGLTEXT_FOUND)
  #  find_package(OpenGLText)
  #endif()
  set( OPENGLTEXT_FOUND "YES" )
  Message(STATUS "--> using package OpenGLText")
  if(OPENGLTEXT_FOUND)
    if(NOT OPENGLTEXT_PATH)
        set(OPENGLTEXT_PATH "${BASE_DIRECTORY}/shared_sources/nv_helpers_gl/OpenGLText")
        # in the library shared_source
        set(OPENGLTEXT_CPP 
          ""
          #"${BASE_DIRECTORY}/shared_sources/nv_helpers_gl/OpenGLText/OpenGLText.cpp"
        )
        set(OPENGLTEXT_H "${BASE_DIRECTORY}/shared_sources/nv_helpers_gl/OpenGLText/OpenGLText.h")
    endif()
    add_definitions(-DUSEOPENGLTEXT)
    set(OGLTEXT
      #now in the shared_source library
      #${BASE_DIRECTORY}/shared_sources/nv_helpers_gl/traceDisplay/tracedisplayogl.cpp
      ${BASE_DIRECTORY}/shared_sources/nv_helpers_gl/traceDisplay/tracedisplay.h
      ${BASE_DIRECTORY}/shared_sources/nv_helpers_gl/traceDisplay/trace.h
      ${BASE_DIRECTORY}/shared_sources/nv_helpers_gl/OpenGLText/arial_10.h
      ${BASE_DIRECTORY}/shared_sources/nv_helpers_gl/OpenGLText/arial_10_bitmap.h
      ${BASE_DIRECTORY}/shared_sources/nv_helpers_gl/OpenGLText/baub_16.h
      ${BASE_DIRECTORY}/shared_sources/nv_helpers_gl/OpenGLText/baub_16_bitmap.h
      ${OPENGLTEXT_CPP}
      ${OPENGLTEXT_H}
    )
    source_group(OpenGLText FILES ${OGLTEXT})
    LIST(APPEND PACKAGE_SOURCE_FILES ${OGLTEXT}) 
    include_directories(${OPENGLTEXT_PATH})
    include_directories(${BASE_DIRECTORY}/shared_sources/nv_helpers_gl/OpenGLText)
    include_directories(${BASE_DIRECTORY}/shared_sources/nv_helpers_gl/traceDisplay)
  endif()
endmacro()
#####################################################################################
# package for multi-threading
#
macro(_add_package_CThread)
  Message(STATUS "--> using package CThread")
  set(CTHREAD_PATH "${BASE_DIRECTORY}/shared_sources/CThread")
  get_directory_property(hasParent PARENT_DIRECTORY)
  if(hasParent)
    set( USING_CTHREAD "YES" PARENT_SCOPE) # PARENT_SCOPE important to have this variable passed to parent. Here we want to notify that something used the Vulkan package
  else()
    set( USING_CTHREAD "YES")
  endif()
  add_definitions(-DUSECTHREAD)
  set(CTHREAD_H
    ${CTHREAD_PATH}/CThread.h
    ${CTHREAD_PATH}/CThreadWork.h
    ${CTHREAD_PATH}/RingBuffer.h
  )
  set(CTHREAD_CPP
    ${CTHREAD_PATH}/CThread.cpp
    ${CTHREAD_PATH}/CThreadWork.cpp
  )
  source_group(CThread FILES ${CTHREAD_H})
  LIST(APPEND PACKAGE_SOURCE_FILES ${CTHREAD_H}) 
  include_directories(${CTHREAD_PATH})
endmacro(_add_package_CThread)
# optional add of this package in the shared lib
macro(_optional_package_CThread)
  if(USING_CTHREAD)
    _add_package_CThread()
  source_group(CThread FILES ${CTHREAD_CPP})
  Message(STATUS "CTHREAD_CPP = ${CTHREAD_CPP}")
  LIST(APPEND PACKAGE_SOURCE_FILES ${CTHREAD_CPP}) 
  endif(USING_CTHREAD)
endmacro(_optional_package_CThread)
#####################################################################################
# package for Sockets: to allow UDP/TCP IP connections
#
macro(_add_package_Sockets)
  _add_package_CThread()
  Message(STATUS "--> using package Sockets")
  set(SOCKETS_PATH "${BASE_DIRECTORY}/shared_sources/nv_sockets")
  get_directory_property(hasParent PARENT_DIRECTORY)
  if(hasParent)
    set( USING_SOCKETS "YES" PARENT_SCOPE) # PARENT_SCOPE important to have this variable passed to parent. Here we want to notify that something used the Vulkan package
  else()
    set( USING_SOCKETS "YES")
  endif()
  add_definitions(-DUSESOCKETS)
  set(SOCKETS_H
    ${SOCKETS_PATH}/socketClient.h
    ${SOCKETS_PATH}/socketSampleMessages.h
    ${SOCKETS_PATH}/socketServer.h
  )
  set(SOCKETS_CPP
    ${SOCKETS_PATH}/socketClient.cpp
    ${SOCKETS_PATH}/socketServer.cpp
    ${SOCKETS_PATH}/socketSampleMessages.cpp
  )
  source_group(Sockets FILES ${SOCKETS_H})
if(WIN32)
  LIST(APPEND LIBRARIES_OPTIMIZED ws2_32 )
  LIST(APPEND LIBRARIES_DEBUG ws2_32 )
  #TODO: for Linux and Android, too !
endif()
  LIST(APPEND PACKAGE_SOURCE_FILES ${SOCKETS_H} )
  include_directories(${SOCKETS_PATH})
endmacro(_add_package_Sockets)
# optional add of this package in the shared lib
macro(_optional_package_Sockets)
  if(USING_SOCKETS)
    _add_package_Sockets()
  source_group(Sockets FILES ${SOCKETS_CPP})
  LIST(APPEND PACKAGE_SOURCE_FILES ${SOCKETS_CPP} )
  endif(USING_SOCKETS)
endmacro(_optional_package_Sockets)
#####################################################################################
# Optional OptiX package
#
macro(_add_package_Optix)
  find_package(Optix)  
  if(OPTIX_FOUND)
      Message(STATUS "--> using package OptiX")
      add_definitions(-DUSEOPTIX)
      include_directories(${OPTIX_INCLUDE_DIR})
    LIST(APPEND LIBRARIES_OPTIMIZED ${OPTIX_LIB} )
      LIST(APPEND LIBRARIES_DEBUG ${OPTIX_LIB} )
      LIST(APPEND PACKAGE_SOURCE_FILES ${OPTIX_HEADERS} )
      source_group(OPTIX FILES  ${OPTIX_HEADERS} )
 else()
     Message(STATUS "--> NOT using package OptiX")
 endif()
endmacro()
#####################################################################################
# Optional VulkanSDK package
#
macro(_add_package_VulkanSDK)
  find_package(VulkanSDK)  
  if(VULKANSDK_FOUND)
      Message(STATUS "--> using package VulkanSDK (version ${VULKANSDK_VERSION})")
      get_directory_property(hasParent PARENT_DIRECTORY)
      if(hasParent)
        set( USING_VULKANSDK "YES" PARENT_SCOPE) # PARENT_SCOPE important to have this variable passed to parent. Here we want to notify that something used the Vulkan package
      else()
        set( USING_VULKANSDK "YES")
      endif()
      add_definitions(-DUSEVULKANSDK)
      include_directories(${VULKANSDK_INCLUDE_DIR})
    LIST(APPEND LIBRARIES_OPTIMIZED ${VULKAN_LIB} )
      LIST(APPEND LIBRARIES_DEBUG ${VULKAN_LIB} )
      LIST(APPEND PACKAGE_SOURCE_FILES ${VULKANSDK_HEADERS} )
      source_group(VULKANSDK FILES  ${VULKANSDK_HEADERS} )
 else()
     Message(STATUS "--> NOT using package VulkanSDK")
 endif()
endmacro()
# this macro is needed for the samples to add this package, although not needed
# this happens when the shared_sources library was built with these stuff in it
# so many samples can share the same library for many purposes
macro(_optional_package_VulkanSDK)
  if(USING_VULKANSDK)
    _add_package_VulkanSDK()
  endif(USING_VULKANSDK)
endmacro(_optional_package_VulkanSDK)
#####################################################################################
# Optional FT-IZB package
#
macro(_add_package_Ftizb)
  find_package(FTIZB)  
  if(FTIZB_FOUND)
      Message(STATUS "--> using package FTIZB")
      add_definitions(-DUSEFTIZB)
      include_directories(${FTIZB_INCLUDE_DIR})
    LIST(APPEND LIBRARIES_OPTIMIZED ${FTIZB_LIB_RELEASE} )
      LIST(APPEND LIBRARIES_DEBUG ${FTIZB_LIB_DEBUG} )
      LIST(APPEND PACKAGE_SOURCE_FILES ${FTIZB_HEADERS} )    
      source_group(FTIZB FILES ${FTIZB_HEADERS} )  
 else()
     Message(STATUS "--> NOT using package OptiX") 
 endif()
endmacro()
#####################################################################################
# Optional CUDA package
# see https://cmake.org/cmake/help/v3.3/module/FindCUDA.html
#
macro(_add_package_Cuda)
  #SET(CUDA_TOOLKIT_ROOT_DIR $ENV{CUDA_BIN_PATH})
  find_package(CUDA QUIET)
  if(CUDA_FOUND)
      Message(STATUS "--> using package CUDA (${CUDA_VERSION})")
      add_definitions(-DUSECUDA)
      include_directories(${CUDA_INCLUDE_DIRS})
      LIST(APPEND LIBRARIES_OPTIMIZED ${CUDA_LIBRARIES} )
      LIST(APPEND LIBRARIES_DEBUG ${CUDA_LIBRARIES} )
      #LIST(APPEND PACKAGE_SOURCE_FILES ${CUDA_HEADERS} ) Not available anymore with cmake 3.3... we might have to list them by hand
      # source_group(CUDA FILES ${CUDA_HEADERS} )  Not available anymore with cmake 3.3
 else()
     Message(STATUS "--> NOT using package CUDA") 
 endif()
endmacro()
#####################################################################################
# Optional OpenCL package
#
macro(_add_package_OpenCL)
  find_package(OpenCL QUIET)  
  if(OpenCL_FOUND)
      Message(STATUS "--> using package OpenCL : ${OpenCL_LIBRARIES}")
      add_definitions(-DUSEOPENCL)
      include_directories(${OpenCL_INCLUDE_DIRS})
      # just do the copy only if we pointed to the local OpenCL package
      string(FIND ${OpenCL_INCLUDE_DIRS} "shared_external" OFFSET)
      if((OFFSET GREATER -1) AND WIN32 )
        if((ARCH STREQUAL "x86"))
          SET(OPENCL_DLL ${BASE_DIRECTORY}/shared_external/OpenCL/lib/x86/OpenCL.dll)
        else()
          SET(OPENCL_DLL ${BASE_DIRECTORY}/shared_external/OpenCL/lib/x64/OpenCL.dll)
        endif()
      endif()
    LIST(APPEND LIBRARIES_OPTIMIZED ${OpenCL_LIBRARIES} )
      LIST(APPEND LIBRARIES_DEBUG ${OpenCL_LIBRARIES} )
 else()
     Message(STATUS "--> NOT using package OpenCL") 
 endif()
endmacro()

#####################################################################################
# NSight
#
# still need the include directory when no use of NSIGHT: for empty #defines
macro(_add_package_NSight)
  Message(STATUS "--> using package NSight")
  include_directories(
      ${BASE_DIRECTORY}/shared_external/NSight
  )
  if(SUPPORT_NVTOOLSEXT)
    link_directories(
        ${BASE_DIRECTORY}/shared_external/NSight
    )
    LIST(APPEND PACKAGE_SOURCE_FILES 
      ${BASE_DIRECTORY}/shared_external/NSight/NSightEvents.h
      ${BASE_DIRECTORY}/shared_external/NSight/nvToolsExt.h
    )
    add_definitions(-DSUPPORT_NVTOOLSEXT)
    if(ARCH STREQUAL "x86")
      SET(NSIGHT_DLL ${BASE_DIRECTORY}/shared_external/NSight/nvToolsExt32_1.dll)
      SET(NSIGHT_LIB ${BASE_DIRECTORY}/shared_external/NSight/nvToolsExt32_1.lib)
    else()
      SET(NSIGHT_DLL ${BASE_DIRECTORY}/shared_external/NSight/nvToolsExt64_1.dll)
      SET(NSIGHT_LIB ${BASE_DIRECTORY}/shared_external/NSight/nvToolsExt64_1.lib)
    endif()
    LIST(APPEND LIBRARIES_OPTIMIZED ${NSIGHT_LIB})
    LIST(APPEND LIBRARIES_DEBUG ${NSIGHT_LIB})
  endif()
endmacro()

#####################################################################################
# Macro that copies various binaries that need to be close to the exe files
#
macro(_copy_binaries_to_target _PROJNAME)

  if(SUPPORT_NVTOOLSEXT)
    _copy_files_to_target( ${_PROJNAME} "${NSIGHT_DLL}")
    install(FILES "${NSIGHT_DLL}" DESTINATION bin)
    install(FILES "${NSIGHT_DLL}" DESTINATION bin_debug)
  endif()

  if(NOT UNIX)
    if(ZLIB_FOUND)
      _copy_files_to_target( ${_PROJNAME} "${ZLIB_BIN}")
      install(FILES "${ZLIB_BIN}" DESTINATION bin)
      install(FILES "${ZLIB_BIN}" DESTINATION bin_debug)
    endif()

    if(SVCMFCUI_FOUND)
        _copy_files_to_target( ${_PROJNAME} "${SVCMFCUI_DLL}")
        install(FILES "${SVCMFCUI_DLL}" DESTINATION bin)
        if(SVCMFCUID_DLL)
          _copy_files_to_target( ${_PROJNAME} "${SVCMFCUID_DLL}")
          install(FILES "${SVCMFCUID_DLL}" DESTINATION bin_debug)
        endif()
   endif()
  endif()
  if(ANTTWEAKBAR_FOUND)
    _copy_files_to_target( ${_PROJNAME} "${ANTTWEAKBAR_DLL}")
    install(FILES "${ANTTWEAKBAR_DLL}" DESTINATION bin)
    install(FILES "${ANTTWEAKBAR_DLL}" DESTINATION bin_debug)
  endif()
  if(OPTIX_FOUND)
    _copy_files_to_target( ${_PROJNAME} "${OPTIX_DLL}")
    install(FILES "${OPTIX_DLL}" DESTINATION bin)
    install(FILES "${OPTIX_DLL}" DESTINATION bin_debug)
  endif()
  if(OPENCL_DLL)
    _copy_files_to_target( ${_PROJNAME} "${OPENCL_DLL}")
    install(FILES "${OPENCL_DLL}" DESTINATION bin)
    install(FILES "${OPENCL_DLL}" DESTINATION bin_debug)
  endif()
  install(TARGETS ${_PROJNAME} DESTINATION bin CONFIGURATIONS Release)
  install(TARGETS ${_PROJNAME} DESTINATION bin_debug CONFIGURATIONS Debug)
endmacro()

include_directories(
    ${BASE_DIRECTORY}/shared_sources
)

#####################################################################################
# Macro to download a file from a URL
#
macro(_download_file _URL _TARGET _FORCE)
  if(${_FORCE} OR (NOT EXISTS ${_TARGET}))
    Message(STATUS "downloading ${_URL} ==> ${_TARGET}")
    file(DOWNLOAD ${_URL} ${_TARGET} SHOW_PROGRESS)
  else()
    Message(STATUS "model ${_TARGET} already loaded...")
  endif()
endmacro()
#
# example: _download_files("${FILELIST}"  "http://..." "${BASE_DIRECTORY}/shared_external/..." ${MODELS_DOWNLOAD_FORCE} )
#
macro(_download_files _FILELIST _URL _TARGET _FORCE )
  foreach(_FILE ${_FILELIST})
    if(${_FORCE} OR (NOT EXISTS "${_TARGET}/${_FILE}"))
      Message(STATUS "*******************************************")
      Message(STATUS "downloading ${_URL}/${_FILE}\n ==>\n ${_TARGET}")
      Message(STATUS "*******************************************")
      file(DOWNLOAD ${_URL}/${_FILE} ${_TARGET}/${_FILE} SHOW_PROGRESS)
    else()
      Message(STATUS "model ${_FILE} already loaded...")
    endif()
  endforeach(_FILE)
endmacro()

#####################################################################################
# Macro to add custom build for Spir-V
# _SOURCE can be more than one file (.vert + .frag)
# _OUTPUT is the .spv file, resulting from the linkage
#
macro(_compile_GLSL _SOURCE _OUTPUT SOURCE_LIST)
  LIST(APPEND ${SOURCE_LIST} ${_SOURCE})
  #Message(STATUS "${GLSLANGVALIDATOR} -o ${_OUTPUT} -V ${_SOURCE}")
  #Message(STATUS "${_OUTPUT} : ${_SOURCE}")
  if(GLSLANGVALIDATOR)
    add_custom_command(
      OUTPUT ${CMAKE_CURRENT_SOURCE_DIR}/${_OUTPUT}
      COMMAND echo ${GLSLANGVALIDATOR} -o ${_OUTPUT} -V ${_SOURCE}
      COMMAND ${GLSLANGVALIDATOR} -o ${_OUTPUT} -V ${_SOURCE}
      MAIN_DEPENDENCY ${_SOURCE}
      WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
      )
  else(GLSLANGVALIDATOR)
    MESSAGE(WARNING "could not find GLSLANGVALIDATOR to compile shaders")
  endif(GLSLANGVALIDATOR)
endmacro()

#####################################################################################
# 
#
macro(_add_package_tinythreadpp)
  Message(STATUS "--> using package TinyThread++")
  set(TINYTHREADPP_DIRECTORY ${BASE_DIRECTORY}/shared_sources/tinythreadpp)
  include_directories(${TINYTHREADPP_DIRECTORY}/src)
  set(PROJECT_TINYTHREADPP ${TINYTHREADPP_DIRECTORY}/src/fast_mutex.h ${TINYTHREADPP_DIRECTORY}/src/tinythread.h ${TINYTHREADPP_DIRECTORY}/src/tinythread.cpp)

  LIST(APPEND PACKAGE_SOURCE_FILES
        ${PROJECT_TINYTHREADPP}
        )
endmacro(_add_package_tinythreadpp)

#####################################################################################
# This is the rest of the cmake code that the project needs to call
# used by the samples via _add_shared_sources_lib and by shared_sources
#
macro(_process_shared_cmake_code)
  #-----------------------------------------------------------------------------------
  #
  set(PLATFORM_LIBRARIES
      ${OPENGL_LIBRARY}
  )
  set(COMMON_SOURCE_FILES)
  LIST(APPEND COMMON_SOURCE_FILES
      ${BASE_DIRECTORY}/shared_sources/resources.h
      ${BASE_DIRECTORY}/shared_sources/resources.rc
  )

  #-----------------------------------------------------------------------------------
  # Glew : source or lib
  if(GLEW_FOUND)
    if(GLEW_SOURCE)
      #message(STATUS "Note: found Glew source code. Will add it in shared_source lib")
      add_definitions(-DGLEW_STATIC)
    else()
        message(STATUS "using GLEW library")
        LIST(APPEND PLATFORM_LIBRARIES ${GLEW_LIBRARY})
    endif()
    add_definitions(-DGLEW_NO_GLU)
  endif(GLEW_FOUND)

  #-----------------------------------------------------------------------------------
  # GLFW library
  if(GLFW_FOUND AND USE_GLFW)
    message(STATUS "using GLFW library ${GLFW_LIBRARY}")
    LIST(APPEND PLATFORM_LIBRARIES ${GLFW_LIBRARY})
    add_definitions(-DUSE_GLFW)
  endif()
    
    
   #-----------------------------------------------------------------------------------
   # XF86
   if(UNIX AND NOT USE_GLFW)
   LIST(APPEND PLATFORM_LIBRARIES "Xxf86vm")
  endif()
endmacro(_process_shared_cmake_code)
#####################################################################################
# This is the rest of the cmake code that the samples needs to call in order
# - to add the shared_sources library (needed by any sample)
# - this part will also setup a directory where to find some downloaded resources
# - add optional packages
# - and will then call another shared cmake macro : _process_shared_cmake_code
#
macro(_add_shared_sources_lib)
  #-----------------------------------------------------------------------------------
  #  now we have added some packages, we can guess more
  # on what is needed overall for the shared library
  if(ADD_SUBDIR_BELOW)
    if(EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/shared_sources/CMakeLists_include.txt)
      add_subdirectory(${CMAKE_CURRENT_SOURCE_DIR}/shared_sources ${CMAKE_BINARY_DIR}/shared_sources)
      # if the resource of downloaded meshes is local, keep this one. Otherwise, use the common one
      if(EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/downloaded_resources)
        SET(DOWNLOAD_TARGET_DIR "${BASE_DIRECTORY}/downloaded_resources")
      else()
        SET(DOWNLOAD_TARGET_DIR "${BASE_DIRECTORY}/../downloaded_resources")
      endif()
    elseif(EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/../../shared_sources)
      add_subdirectory(${CMAKE_CURRENT_SOURCE_DIR}/../../shared_sources ${CMAKE_BINARY_DIR}/shared_sources)
      SET(DOWNLOAD_TARGET_DIR "${BASE_DIRECTORY}/downloaded_resources")
    else()
      add_subdirectory(${CMAKE_CURRENT_SOURCE_DIR}/../shared_sources ${CMAKE_BINARY_DIR}/shared_sources)
      SET(DOWNLOAD_TARGET_DIR "${BASE_DIRECTORY}/downloaded_resources")
    endif()
  else()
    SET(DOWNLOAD_TARGET_DIR "${BASE_DIRECTORY}/downloaded_resources")
  endif()
  #-----------------------------------------------------------------------------------
  # optional packages we don't need, but might be needed by other samples
  Message(STATUS " Packages needed for shared_sources lib compat:")
  if(USING_OPENGL OR NOT OPENGL_FOUND)
    _optional_package_OpenGL()
  endif()
  if(USING_VULKANSDK OR NOT VULKANSDK_FOUND)
    _optional_package_VulkanSDK()
  endif()
  # if socket system required in samples, add the package
  if(SUPPORT_SOCKETS)
    _add_package_Sockets()
  endif()
  # finish with another part (also used by cname for the shared_sources)
  _process_shared_cmake_code()
endmacro()
